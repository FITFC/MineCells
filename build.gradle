import com.modrinth.minotaur.dependencies.ModDependency

plugins {
    id "fabric-loom" version "0.11-SNAPSHOT"
    id "maven-publish"
    id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/minecells.accesswidener")
}

modrinth {
    projectId.set("9Qdvz1OV")
    uploadFile.set(remapJar)
    versionType.set("${project.mod_version_type}")
    versionName.set("minecells-${versionType}-${project.mod_version}")
    changelog.set(rootProject.file("changelogs/minecells_${project.mod_version}.md").text)
    syncBodyFrom.set(rootProject.file("project/body.md").text)

    dependencies.set([
        new ModDependency("P7dR8mSH", "required")
    ])
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    include("com.github.Draylar.omega-config:omega-config-base:${project.omega_config_version}")
    modImplementation("com.github.Draylar.omega-config:omega-config-base:${project.omega_config_version}")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}